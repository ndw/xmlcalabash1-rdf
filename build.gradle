buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    classpath group: 'net.sf.saxon', name: 'Saxon-HE', version: saxonVersion
  }
}

plugins {
  id "java"
  id "maven-publish"
  id "signing"
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://maven.restlet.org" }
}

configurations {
  copydep {
    extendsFrom runtimeClasspath
  }
  copydep.exclude module: 'xmlcalabash'
  copydep.exclude module: 'nwalsh-annotations'
  copydep.exclude module: 'httpclient'
  copydep.exclude module: 'httpclient-cache'
  copydep.exclude module: 'httpcore'
  /* Exclude logging artifacts so they don't duplicate what's in XML Calabash */
  copydep.exclude module: 'jcl-over-slf4j'
  copydep.exclude module: 'log4j'
  copydep.exclude module: 'log4j-api'
  copydep.exclude module: 'log4j-core'
  copydep.exclude module: 'log4j-slf4j-impl'
  copydep.exclude module: 'org.restlet.ext.slf4j'
  copydep.exclude module: 'slf4j-api'
  copydep.exclude module: 'slf4j-log4j12'
}

dependencies {
  implementation (
    [group: 'com.nwalsh', name: 'nwalsh-annotations', version: '1.0.1'],
    [group: 'com.xmlcalabash', name: 'xmlcalabash', version: xmlCalabashVersion],
    [group: 'org.apache.jena', name: 'jena', version: '2.12.1' ],
    [group: 'org.apache.jena', name: 'jena-arq', version: '2.12.1' ],
    [group: 'org.apache.jena', name: 'jena-core', version: '2.12.1' ],
    [group: 'org.apache.jena', name: 'jena-iri', version: '1.1.1' ],
    [group: 'org.semarglproject', name: 'semargl-core', version: '0.6.1'],
    [group: 'org.semarglproject', name: 'semargl-rdfa', version: '0.6.1']
  )
}

// Variables
project.ext.saxonRelease = saxonVersion.substring(0,5)
project.ext.saxonBranch  = saxonVersion.substring(0,3).replaceAll("\\.", "")
project.ext.releaseVersion = relVersion
project.ext.distVersion = relVersion + snapshot
relVersion = project.ext.distVersion // - gets turned into . in some contexts

compileJava {
  options.deprecation = true
  options.compilerArgs += ['-Xlint:deprecation']
  options.annotationProcessorPath = configurations.compileClasspath
}

task copyLib(type: Copy) {
  FileCollection lib = configurations.copydep
  String path = ""
  lib.each {
    File file -> path += " lib/" + file.name
  }
  project.ext.runtimeClasspath = path.trim()

  from lib
  into { "build/dist/lib" }
}

jar {
  archiveBaseName = "xmlcalabash1-rdf-" + relVersion
  manifest {
    attributes 'Built-By': builtBy
    attributes 'Implementation-Vendor': 'Norman Walsh'
    attributes 'Implementation-Title': 'XML Calabash RDF Steps'
    attributes 'Implementation-Version': distVersion
    attributes 'Bundle-SymbolicName': 'com.xmlcalabash.extensions.rdf'
    attributes 'Bundle-RequiredExecutionEnvironment': 'J2SE-1.7'
    attributes 'Export-Package': '*, etc'
    attributes 'Import-Package': 'net.sf.saxon.*;version=' + project.ext.saxonRelease + ',\
  javax.crypto.*,\
  javax.xml.*,\
  org.apache.commons.httpclient.*,\
  org.xml.sax.*,\
  *;resolution:=optional'
    attributes 'DynamicImport-Package': '*'
    attributes 'Class-Path': project.ext.runtimeClasspath + " lib"
  }
}

task copyNotices(type: Copy) {
  from 'resources/notices'
  into 'build/dist/docs/notices'
}
jar.dependsOn copyNotices

task copyStuff(type: Copy) {
  from 'README.md'
  from 'LICENSE.md'
  into 'build/dist'
}

task copyToDist(dependsOn: [copyLib,copyStuff]) {
  // nop
}
jar.dependsOn copyToDist

task javadocJar(type: Jar, dependsOn: javadoc) {
    from tasks.javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task copyJar(dependsOn: jar, type: Copy) {
  from "build/libs/xmlcalabash1-rdf-" + distVersion + ".jar"
  into "build/dist"
}

task copyJavaDoc(dependsOn: javadoc, type: Copy) {
  from "build/docs"
  into "build/dist/docs"
}

task testStep(type: JavaExec) {
  classpath = configurations.runtimeClasspath + sourceSets.main.output

  main = 'com.xmlcalabash.drivers.Main'
  maxHeapSize = "1024m"
  args('-D', '-a', 'src/test/resources/test-pipeline.xpl')
}

task makeDist(dependsOn: [ build, copyJar, copyLib ]) {
  doLast {
    println("Created distribution in build/dist")
  }
}

task zipDist(dependsOn: makeDist, type: Zip) {
  from('build/dist')
  into 'xmlcalabash1-rdf-' + distVersion
  archiveFileName = 'xmlcalabash1-rdf-' + distVersion + ".zip"
}

task dist(dependsOn: [testStep, zipDist]) {
  // nop
}

signing {
  sign publishing.publications
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'XML Calabash MarkLogic RDF Steps'
        packaging = 'jar'
        description = 'XML Calabash extension steps to load/query RDF'
        url = 'https://github.com/ndw/xmlcalabash1-rdf'

        scm {
          url = 'scm:git@github.com:ndw/xmlcalabash1-rdf.git'
          connection = 'scm:git@github.com:ndw/xmlcalabash1-rdf.git'
          developerConnection = 'scm:git@github.com:ndw/xmlcalabash1-rdf.git'
        }

        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'ndw'
            name = 'Norman Walsh'
          }
        }
      }

      groupId = "com.xmlcalabash"
      artifactId = "xmlcalabash1-rdf"
      version = relVersion
      from components.java
      artifact javadocJar
      artifact sourcesJar
    }
  }

  repositories {
    maven {
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = sonatypeUsername
        password = sonatypePassword
      }
    }
  }
}

